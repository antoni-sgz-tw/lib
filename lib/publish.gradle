apply plugin: 'maven-publish'

def githubProperties = new Properties()
githubProperties.load(
        new FileInputStream(rootProject.file("github_credentials.properties"))
)

publishing {
    publications {
        mycheckoutlib(MavenPublication) {
            groupId 'com.sgz.antoni'
            artifactId "lib"
            version "1.0.2"
            artifact("$buildDir/outputs/aar/${getArtifactId()}-debug.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    if (dependency.name != 'unspecified' && dependency.version != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/antoni-sgz-tw/lib")
            credentials {
                username = githubProperties['github.username'] ?: System.getenv("GITHUB_USERNAME")
                password = githubProperties['github.password'] ?: System.getenv("GITHUB_API_KEY")
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from android.sourceSets.main.java.srcDirs
}